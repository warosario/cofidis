/*
Exercício 45 - PROC FORECAST

Criar uma tabela de pedidos particionada e ordenada pelo ultimo ano de informação.
Criar um index na tabela particionada criada acima pelo campo cliente_id.
Criar uma previsão de pedidos para os próximos 6 meses por cliente_id
Salvar o resultado na libname formacao
*/

PROC SQL;
  CREATE TABLE AUX_PEDIDOS_FORECAST AS
  SELECT
    CLIENTE_ID,
    MDY(MONTH(DATA_PEDIDO), 1, YEAR(DATA_PEDIDO)) as DATA_FORECAST FORMAT=date9.,
    COUNT(PEDIDO_ID) as TOTAL_PEDIDOS
  FROM FORMACAO.PEDIDOS
  WHERE YEAR(DATA_PEDIDO) = (SELECT MAX(YEAR(DATA_PEDIDO)) from FORMACAO.PEDIDOS)
  GROUP BY CLIENTE_ID,CALCULATED DATA_FORECAST
  ORDER BY CLIENTE_ID, CALCULATED DATA_FORECAST;
QUIT;

PROC SQL;

  CREATE TABLE PEDIDOS_FORECAST AS
  SELECT *
    FROM AUX_PEDIDOS_FORECAST
  GROUP BY CLIENTE_ID
  HAVING COUNT(1)>3;

QUIT;

PROC DATASETS LIB=WORK;
  MODIFY PEDIDOS_FORECAST;
  INDEX CREATE CLIENTE_ID;
QUIT;

PROC FORECAST DATA=PEDIDOS_FORECAST OUT=FORMACAO.PEDIDOS_FORECAST LEAD=6 INTERVAL=MONTH METHOD=EXPO;
  ID DATA_FORECAST;
  BY CLIENTE_ID;
  VAR TOTAL_PEDIDOS;
RUN;
